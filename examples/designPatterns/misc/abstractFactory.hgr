"""
 Abstract Factory

 Author: Irwin Rodr√≠guez <rodriguez.irwin@gmail.com>
 Date: 2023-02-27
"""
#-----------------------------------------------------#
# Abstract Factory
#-----------------------------------------------------#
def cApplication
    oButton = null
    oCheckbox = null

    pInit(poFactory)
        @oButton = poFactory.fCreateButton()
        @oCheckbox = poFactory.fCreateCheckbox()    
    end

    pPaint()
        @oButton.pPaint()
        @oCheckbox.pPaint()
    end

end

#-----------------------------------------------------#
# Button
#-----------------------------------------------------#
def cButton
    -pPaint()
end

#-----------------------------------------------------#
# MacOS Button
#-----------------------------------------------------#
def cMacOSButton as cButton
    pPaint()
        pPrintLn("You have created MacOSButton.")
    end
end

#-----------------------------------------------------#
# Windows Button
#-----------------------------------------------------#
def cWindowsButton as cButton
    pPaint()
        pPrintLn("You have created WindowsButton.")
    end
end

#-----------------------------------------------------#
# Checkbox
#-----------------------------------------------------#
def cCheckbox
    -pPaint()
end

#-----------------------------------------------------#
# MacOS Checkbox
#-----------------------------------------------------#
def cMacOSCheckbox as cCheckbox
    pPaint()
        pPrintLn("You have created MacOSCheckbox.")
    end
end

#-----------------------------------------------------#
# Windows Checkbox
#-----------------------------------------------------#
def cWindowsCheckbox as cCheckbox
    pPaint()
        pPrintLn("You have created WindowsCheckbox.")
    end
end

#-----------------------------------------------------#
# Factory
#-----------------------------------------------------#
def cGUIFactory
    -fCreateButton()
    -fCreateCheckbox()
end

#-----------------------------------------------------#
# MacOS Factory
#-----------------------------------------------------#
def cMacOSFactory as cGUIFactory
    fCreateButton()
        return new cMacOSButton()
    end

    fCreateCheckbox()
        return new cMacOSCheckbox()
    end
end

#-----------------------------------------------------#
# Windows Factory
#-----------------------------------------------------#
def cWindowsFactory as cGUIFactory
    fCreateButton()
        return new cWindowsButton()
    end

    fCreateCheckbox()
        return new cWindowsCheckbox()
    end
end

#-----------------------------------------------------#
# Main
#-----------------------------------------------------#
def fConfigureApplication
    let oApplication = null, oFactory = null    
    let sName = fReadLn("Enter the name of OS you are using (MacOS or Windows): ")

    if sName == "MacOS"
        oFactory = new cMacOSFactory()
    else
        oFactory = new cWindowsFactory()
    end
    oApplication = new cApplication(oFactory)

    return oApplication
end

let oApp = fConfigureApplication()
oApp.pPaint()


# final assert to automatically check if all tests passed
pAssert(true, "This test should always pass")