"""
 Command

 Author: Irwin Rodr√≠guez <rodriguez.irwin@gmail.com>
 Date: 2023-02-28
"""

# cOrder class (acts as an interface)
def cOder
    -pExecute()
end

# cStock class: a request class
def cStock
    sName = "ABC"
    nQuantity = 10

    pBuy
        println("Stock [Name: " & @sName & ", Quantity: " & @nQuantity & "] bought")
    end

    pSell
        println("Stock [Name: " & @sName & ", Quantity: " & @nQuantity & "] sold")
    end
end

# cBuyStock class: a concrete command class
def cBuyStock as cOder
    oAbcStock = null

    pInit(poAbcStock)
        @oAbcStock = poAbcStock
    end

    pExecute
        @oAbcStock.pBuy()
    end
end

# cSellStock class: a concrete command class
def cSellStock as cOder
    oAbcStock = null

    pInit(poAbcStock)
        @oAbcStock = poAbcStock
    end

    pExecute
        @oAbcStock.pSell()
    end
end

# cBroker class: a invoker class
def cBroker
    aOrderList = []

    pTakeOrder(poOrder)
        @aOrderList.push(poOrder)
    end

    pPlaceOrders
        for each vOrder in @aOrderList
            vOrder.pExecute()
        end
        @aOrderList.clear()
    end
end

# lpMain procedure
def pMain
    let oAbcStock = new cStock()
    let oBuyStockOrder = new cBuyStock(oAbcStock)
    let oSellStockOrder = new cSellStock(oAbcStock)

    let oBroker = new cBroker()
    oBroker.pTakeOrder(oBuyStockOrder)
    oBroker.pTakeOrder(oSellStockOrder)

    oBroker.pPlaceOrders()
end

pMain()