"""
 Strategy pattern

 Author: Irwin Rodr√≠guez <rodriguez.irwin@gmail.com>
 Date: 2023-02-26
"""

#-------------------------------------------#
# Context class
#-------------------------------------------#
def cFinancialCalculator
    nBalance = 0.00
    nRate = 0.00
    nPeriod = 0
    sType = ""

    fGetInterestAmount
        let oStrategy = new object("c" & @sType)
        return oStrategy.fCalculateInterest(@nBalance, @nRate, @nPeriod)
    end
end

#-------------------------------------------#
# Strategy Classes
#-------------------------------------------#

# Interface class
def cCalculateInterest
    -fCalculateInterest()
end

# Implementation classes (Concrete Strategies)
def cSimple as cCalculateInterest
    fCalculateInterest(pnBalance, pnRate, pnPeriod)
        let nInterest = pnBalance * ((pnRate / 100) / 365) * pnPeriod
        return nInterest
    end
end

def cQuarterly as cCalculateInterest
    fCalculateInterest(pnBalance, pnRate, pnPeriod)
        let nInterest = 0.0
        for nI = 1 to pnPeriod
            nInterest += (pnBalance + nInterest) * ((pnRate / 100) / 4)
        end
        return nInterest
    end
end

def cMonthly as cCalculateInterest
    fCalculateInterest(pnBalance, pnRate, pnPeriod)
        let nInterest = 0.0
        for nI = 1 to pnPeriod
            nInterest += (pnBalance + nInterest) * ((pnRate / 100) / 12)
        end
        return nInterest
    end
end

def cDaily as cCalculateInterest
    fCalculateInterest(pnBalance, pnRate, pnPeriod)
        let nInterest = 0.0
        for nI = 1 to pnPeriod
            nInterest += (pnBalance + nInterest) * ((pnRate / 100) / 365)
        end
        return nInterest
    end
end

#-------------------------------------------#
# Client code
#-------------------------------------------#
let oCalculator = new object("cFinancialCalculator")
oCalculator.nBalance = 1000.00
oCalculator.nRate = 5.00
oCalculator.nPeriod = 365

# Simple interest
oCalculator.sType = "Simple"
pPrintLn(oCalculator.fGetInterestAmount())

# Quarterly interest
oCalculator.sType = "Quarterly"
pPrintLn(oCalculator.fGetInterestAmount())

# Monthly interest
oCalculator.sType = "Monthly"
pPrintLn(oCalculator.fGetInterestAmount())

# Daily interest
oCalculator.sType = "Daily"
pPrintLn(oCalculator.fGetInterestAmount())

#-------------------------------------------#
# End
#-------------------------------------------#