"""
 Factory Pattern

 Author: Irwin Rodr√≠guez <rodriguez.irwin@gmail.com>
 Date: 2023-02-27
"""

#-----------------------------------------#
# Product class
#-----------------------------------------#
def cCoffee
    sName = ""

    -pPrepare()

    pServe
        pPrintLn("Here\'s your cup of " & @sName & ". Enjoy!")
    end
end

def cBrewedCoffee as cCoffee
    sName = "Brewed Coffee"
    pPrepare
        pPrintLn("choose coffee")
        pPrintLn("put in brew machine")
        pPrintLn("add water")
        pPrintLn("brew")
    end
end

def cSpecialtyCoffee as cCoffee
    sName = "Specialty Coffee"
    pPrepare
        pPrintLn("choose coffee")
        pPrintLn("put in espresso machine")
        pPrintLn("pull expresso shot")
        pPrintLn("choose milk")
        pPrintLn("steam milk")
        pPrintLn("add cocoa")
        pPrintLn("mix milk and coffee")
    end
end

#-----------------------------------------#
# Creator class
#-----------------------------------------#
def cCafe
    oProduct = null
    -pMakeCoffee()
end

def cMyCafe as cCafe
    pMakeCoffee(psType)
        @oProduct = fGetCoffee(psType)
        @oProduct.pPrepare()
        @oProduct.pServe()
    end

    fGetCoffee(psType)
        return iif(psType == "brewed", new cBrewedCoffee(), new cSpecialtyCoffee())
    end
end

#-----------------------------------------#
# Client
#-----------------------------------------#
let oCafe = new cMyCafe()
oCafe.pMakeCoffee("brewed")

# Output
# choose coffee
# put in brew machine
# add water
# brew
# Here's your cup of Brewed Coffee. Enjoy!

oCafe.pMakeCoffee("mocha")

# final assert to automatically check if all tests passed
pAssert(true, "This test should always pass")