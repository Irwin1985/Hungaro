"""
    Original code: https://www.tutorialspoint.com/design_pattern/index.htm
    Design patterns represent the best practices used by experienced object-oriented software 
    developers. Design patterns are solutions to general problems that software developers
    faced during software development. These solutions were obtained by trial and error by 
    numerous software developers over quite a substantial period of time.

    Pattern: Chain of Responsibility
    Description:
        Chain of responsibility is used to achieve loose coupling in software design where a 
        request from the client is passed to a chain of objects to process them. Then the 
        object in the chain will decide themselves who will be processing the request and 
        whether the request is required to be sent to the next object in the chain or not.
        

    Hungaro code writer: Irwin Rodr√≠guez <rodriguez.irwin@gmail.com>
    Date: 2023-02-28
"""

def cAbstractLogger
    nInfo = 1
    nDebug = 2
    nError = 3
    nLevel = 0

    # next element in chain or responsibility
    oNextLogger = null

    pSetNextLogger(poNextLogger)
        @oNextLogger = poNextLogger
    end

    pLogMessage(pnLevel, psMessage)        
        if @nLevel <= pnLevel
            pWrite(psMessage)
        end
        if @oNextLogger != null            
            @oNextLogger.pLogMessage(pnLevel, psMessage)
        end
    end

    -pWrite(psMessage)
end

def cConsoleLogger as cAbstractLogger    

    pInit(pnLevel)
        @nLevel = pnLevel
    end

    pWrite(psMessage)
        pPrintLn("Standard Console::Logger: " & psMessage)
    end
end

def cErrorLogger as cAbstractLogger

    pInit(pnLevel)
        @nLevel = pnLevel
    end

    pWrite(psMessage)
        pPrintLn("Error Console::Logger: " & psMessage)
    end
end

def cFileLogger as cAbstractLogger

    pInit(pnLevel)
        @nLevel = pnLevel
    end

    pWrite(psMessage)
        pPrintLn("File::Logger: " & psMessage)
    end
end

def pMain
    let oAbstractLogger = new cAbstractLogger()

    def fGetChainOfLoggers
        let oErrorLogger = new cErrorLogger(oAbstractLogger.nError)
        let oFileLogger = new cFileLogger(oAbstractLogger.nDebug)
        let oConsoleLogger = new cConsoleLogger(oAbstractLogger.nInfo)

        oErrorLogger.pSetNextLogger(oFileLogger)
        oFileLogger.pSetNextLogger(oConsoleLogger)

        return oErrorLogger
    end

    let oLoggerChain = fGetChainOfLoggers()        

    oLoggerChain.pLogMessage(oAbstractLogger.nInfo, "This is an information.")
    oLoggerChain.pLogMessage(oAbstractLogger.nDebug, "This is an debug level information.")
    oLoggerChain.pLogMessage(oAbstractLogger.nError, "This is an error information.")
end

pMain()

# Output:
# Standard Console::Logger: This is an information.
# File::Logger: This is an debug level information.
# Standard Console::Logger: This is an debug level information.
# Error Console::Logger: This is an error information.
# File::Logger: This is an error information.
# Standard Console::Logger: This is an error information.