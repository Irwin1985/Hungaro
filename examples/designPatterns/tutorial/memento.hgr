"""
    Original code: https://www.tutorialspoint.com/design_pattern/index.htm
    Design patterns represent the best practices used by experienced object-oriented software 
    developers. Design patterns are solutions to general problems that software developers
    faced during software development. These solutions were obtained by trial and error by 
    numerous software developers over quite a substantial period of time.

    Pattern: Memento
    Description:
        Memento pattern uses three actor classes. Memento contains state of an object 
        to be restored. Originator creates and stores states in Memento objects and Caretaker 
        object is responsible to restore object state from Memento. We have created 
        classes Memento, Originator and CareTaker.

    Hungaro code writer: Irwin Rodr√≠guez <rodriguez.irwin@gmail.com>
    Date: 2023-02-28
"""

def cMememto
    sState = ""

    pInit(psState)
        poThis.sState = psState
    end

    fGetState()
        return poThis.sState
    end
end

def cOriginator
    sState = ""

    pSetState(psState)
        poThis.sState = psState
    end

    fGetState()
        return poThis.sState
    end

    fSaveStateToMemento()
        return new cMememto(poThis.sState)
    end

    pGetStateFromMemento(poMemento)
        poThis.sState = poMemento.fGetState()
    end
end

def cCareTaker
    aMementoList = []

    pAdd(poState)
        poThis.aMementoList.push(poState)
    end

    fGet(pnIndex)
        return poThis.aMementoList[pnIndex]
    end
end

def pMain
    let oOriginator = new cOriginator()
    let oCareTaker = new cCareTaker()

    oOriginator.pSetState("State #1")
    oOriginator.pSetState("State #2")
    oCareTaker.pAdd(oOriginator.fSaveStateToMemento())

    oOriginator.pSetState("State #3")
    oCareTaker.pAdd(oOriginator.fSaveStateToMemento())

    oOriginator.pSetState("State #4")
    println("Current State: " & oOriginator.fGetState())

    oOriginator.pGetStateFromMemento(oCareTaker.fGet(0))
    println("First saved State: " & oOriginator.fGetState())
    oOriginator.pGetStateFromMemento(oCareTaker.fGet(1))
    println("Second saved State: " & oOriginator.fGetState())
end

pMain()

# Output
# Current State: State #4
# First saved State: State #2
# Second saved State: State #3
